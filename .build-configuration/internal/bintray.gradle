apply plugin: "com.jfrog.bintray"
apply plugin: "maven"
apply plugin: "maven-publish"


task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

ext.artifactId = project.name

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            groupId this.group
            artifactId this.artifactId
            version this.version
            artifact sourcesJar
            artifact javadocJar
            pom {
                name = this.artifactId
                licenses {
                    license {
                        name = "The Apache License, Version 2.0"
                        url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }
                developers {
                    developer {
                        name = "Christoph Hennemann"
                    }
                }
            }
        }
    }
}



bintray {
    user = 'chennemann'
    key = "$bintrayApiKey"
    publications = ['maven']
    publish = true
    pkg {
        repo = 'maven'
        name = this.artifactId
        licenses = ['Apache-2.0']
        vcsUrl = "https://github.com/chennemann/utils.git"
        version {
            name = this.version
        }
    }
}

task publishArtifact {
    group 'publishing'

    dependsOn 'clean'
    dependsOn 'build'
    dependsOn 'sourcesJar'
    dependsOn 'javadocJar'
    dependsOn 'generatePomFileForMavenPublication'
    dependsOn 'bintrayUpload'

    tasks.findByName('build').mustRunAfter 'clean'
    tasks.findByName('sourcesJar').mustRunAfter 'build'
    tasks.findByName('javadocJar').mustRunAfter 'build'
    tasks.findByName('generatePomFileForMavenPublication').mustRunAfter 'sourcesJar'
    tasks.findByName('generatePomFileForMavenPublication').mustRunAfter 'javadocJar'
    tasks.findByName('bintrayUpload').mustRunAfter 'generatePomFileForMavenPublication'
}